#workflow.container("docker://jverwilt/totalrna:v0.5")

import os
import pandas as pd


configfile: "config/config.yml"
path = config['output_dir']
inputpath = config['input_dir']
# Only directories = actual sample folders
samples = [d for d in os.listdir(inputpath) if os.path.isdir(os.path.join(inputpath, d))]


rule all: 
    input:
        expand(os.path.join(path, "{sample}/08_rseqc/{sample}_dedup_bam_stat.txt"), sample = samples),
        expand(os.path.join(path, "{sample}/08_rseqc/{sample}_raw_bam_stat.txt"), sample = samples),
        expand(os.path.join(path, "{sample}/08_rseqc/{sample}_strandedness.txt"), sample = samples),
        expand(os.path.join(path, "{sample}/09_fastqc/{sample}.Aligned.sortedByCoord.out.dedup_fastqc.html"), sample = samples),
        expand(os.path.join(path, "{sample}/05_count/{sample}_htseq_dedup_counts.txt"), sample = samples),
        os.path.join(path, "QC.html")

if config['sequencing'] in ["se"]:
    rule combine_se:
        output:
            R1=os.path.join(path, "{sample}/01_combine/{sample}.fastq.gz")
        params:
            inputpath = inputpath
        shell:
            """
            mkdir -p $(dirname {output.R1})
            cat {params.inputpath}/{wildcards.sample}/*.fastq.gz > {output.R1}
            """

if config['sequencing'] in ["pe"]:
    rule combine_pe:
        output:
            R1=os.path.join(path, "{sample}/01_combine/{sample}_R1.fastq.gz"),
            R2=os.path.join(path, "{sample}/01_combine/{sample}_R2.fastq.gz")
        params:
            inputpath = inputpath
        shell:
            """
            mkdir -p $(dirname {output.R1})
            mkdir -p $(dirname {output.R2})
            cat {params.inputpath}/{wildcards.sample}/*R1*.fastq.gz > {output.R1}
            cat {params.inputpath}/{wildcards.sample}/*R2*.fastq.gz > {output.R2}
            """

    rule extract_umi:
        container: config["dockercontainer"]
        input:
            R1=os.path.join(path, "{sample}/01_combine/{sample}_R1.fastq.gz"),
            R2=os.path.join(path, "{sample}/01_combine/{sample}_R2.fastq.gz")
        output:
            R1=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_R1.fastq.gz"),
            R2=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_R2.fastq.gz"),
            umilog=os.path.join(path, "{sample}/02_extract_umi/UMIextract.log")
        shell:
            """
            mkdir -p $(dirname {output.R1})
            mkdir -p $(dirname {output.R2})
            umi_tools extract --extract-method=string -I {input.R2} -L {output.umilog} --bc-pattern=NNNNNNNN --read2-in={input.R1} -S {output.R2} --read2-out={output.R1}
            """

    rule cut:
        container: config["dockercontainer"]
        input:
            R2=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_R2.fastq.gz")
        output:
            R2=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_trimmed_R2.fastq.gz")
        shell:
            """
            mkdir -p $(dirname {output.R2})
            cutadapt -u 6 -o {output.R2} {input.R2}
            """

    rule map_pe:
        container: config["dockercontainer"]
        input:
            R1=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_R1.fastq.gz"),
            R2=os.path.join(path, "{sample}/02_extract_umi/{sample}_umi_trimmed_R2.fastq.gz")
        output:
            bam=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam")
        params:
            path = path
        resources:
            mem_mb = config["resources"]["star_mem_mb"]
        shell:
            """
            mkdir -p $(dirname {output.bam})
            STAR --chimSegmentMin 10 --runThreadN 2 --genomeDir {config[index][star]} --readFilesCommand gunzip -c --outSAMtype BAM SortedByCoordinate --outFileNamePrefix {params.path}/{wildcards.sample}/03_map/{wildcards.sample}. --readFilesIn {input.R1} {input.R2} --outTmpDir {resources.tmpdir}/temp_star_{wildcards.sample}
            """

elif config['sequencing'] in ["se"]:
    rule map_se:
        container: config["dockercontainer"]
        input:
            R1=os.path.join(path, "{sample}/01_combine/{sample}.fastq.gz"),
        output:
            bam=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam")
        params:
            path = path
        resources:
            mem_mb = config["resources"]["star_mem_mb"]
        shell:
            """
            mkdir -p $(dirname {output.bam})
            STAR --chimSegmentMin 10 --runThreadN 2 --genomeDir {config[index][star]} --readFilesCommand gunzip -c --outSAMtype BAM SortedByCoordinate --outFileNamePrefix {params.path}/{wildcards.sample}/03_map/{wildcards.sample}. --readFilesIn {input.R1} --outTmpDir {resources.tmpdir}/temp_star_{wildcards.sample}
            """

rule index:
    container: config["dockercontainer"]
    input:
        os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam")
    output:
        os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam.bai")
    shell:
        "samtools index {input}"

if config['sequencing'] in ["pe"]:
    rule dedup_umi:
        container: config["dockercontainer"]
        input:
            bam=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam"),
            bami=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam.bai")
        output:
            bam=os.path.join(path, "{sample}/04_dedup/{sample}.Aligned.sortedByCoord.out.dedup.bam"),
            log=os.path.join(path, "{sample}/04_dedup/umidedup.txt")
        params: 
            path = path
        shell:
            """
            mkdir -p $(dirname {output.bam})
            umi_tools dedup -I {input.bam} --output-stats={params.path}/{wildcards.sample}/04_dedup/deduplicated --paired -S {output.bam} -L {output.log}
            """

elif config['sequencing'] in ["se"]:
    rule dedup_picard:
        container: config["dockercontainer"]
        input:
            bam=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam")
        output:
            bam=os.path.join(path, "{sample}/04_dedup/{sample}.Aligned.sortedByCoord.out.dedup.bam"),
            log=os.path.join(path, "{sample}/04_dedup/picarddedup.txt")
        params: 
            path = path
        shell:
            """
            mkdir -p $(dirname {output.bam})
            picard AddOrReplaceReadGroups I={input.bam} O={wildcards.sample}_temp.bam RGID={wildcards.sample} RGLB=lib1 RGPL=ILLUMINA RGPU=unit1 RGSM={wildcards.sample} SORT_ORDER=coordinate
            picard MarkDuplicates I={wildcards.sample}_temp.bam O={output.bam} ASSUME_SORT_ORDER=coordinate VALIDATION_STRINGENCY=SILENT REMOVE_DUPLICATES=true M={output.log}
            rm {wildcards.sample}_temp.bam
            """

rule count:
    container: config["dockercontainer"]
    input:
        os.path.join(path, "{sample}/04_dedup/{sample}.Aligned.sortedByCoord.out.dedup.bam")
    output:
        countsfile=os.path.join(path, "{sample}/05_count/{sample}_htseq_dedup_counts.txt")
    shell:
        """
        mkdir -p $(dirname {output.countsfile})
        htseq-count --format bam --order pos --nonunique none --stranded reverse {input} {config[reference][gtf]} > {output.countsfile}
        """

rule rseqc:
    container: config["dockercontainer"]
    input:
        dedup=os.path.join(path, "{sample}/04_dedup/{sample}.Aligned.sortedByCoord.out.dedup.bam"),
        raw=os.path.join(path, "{sample}/03_map/{sample}.Aligned.sortedByCoord.out.bam")
    output:
        bam_stat_dedup=os.path.join(path, "{sample}/08_rseqc/{sample}_dedup_bam_stat.txt"),
        bam_stat_raw=os.path.join(path, "{sample}/08_rseqc/{sample}_raw_bam_stat.txt"),
        strandedness=os.path.join(path, "{sample}/08_rseqc/{sample}_strandedness.txt")
    shell:
        """
        mkdir -p $(dirname {output.strandedness})
        bam_stat.py -i {input.dedup} > {output.bam_stat_dedup}
        bam_stat.py -i {input.raw} > {output.bam_stat_raw}
        infer_experiment.py -r {config[reference][bed]} -i {input.raw} > {output.strandedness}
        """

rule fastqc:
    container: config["dockercontainer"]
    input:
        os.path.join(path, "{sample}/08_rseqc/{sample}_strandedness.txt")
    output:
        fastqcfile=os.path.join(path, "{sample}/09_fastqc/{sample}.Aligned.sortedByCoord.out.dedup_fastqc.html")
    params:
        path = path
    shell:
        """
        mkdir -p $(dirname {output.fastqcfile})
        fastqc -o {params.path}/{wildcards.sample}/09_fastqc/ {params.path}/{wildcards.sample}/01_combine/* {params.path}/{wildcards.sample}/04_dedup/*.bam
        """

rule DNA_contamination:
    container: config["dockercontainer"]
    input: 
        fastqc=expand(os.path.join(path, "{sample}/09_fastqc/{sample}.Aligned.sortedByCoord.out.dedup_fastqc.html"), sample = samples),
        counts=expand(os.path.join(path, "{sample}/05_count/{sample}_htseq_dedup_counts.txt"), sample = samples)
    output:
        os.path.join(path, "QC.html")
    params:
        path = path,
        sequencing=config["sequencing"]
    shell:
        "Rscript --vanilla workflow/scripts/write_rmarkdown.R {params.path} {output} {params.sequencing}"


